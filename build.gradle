plugins{
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'java-library'
}

ext{
    caffeine_version = '3.0.2'
    evalex_version = '2.5'
    lavaplayer_version = '1.3.77'

    servicer_version = '1.0.3'
    immutables_version = '2.8.2'
    jackson_version = '2.12.3'

    junit_version = '5.7.1'
}

allprojects{
    apply plugin: 'java-library'
    apply plugin: 'com.github.ben-manes.versions'

    targetCompatibility = 16
    sourceCompatibility = 16

    repositories{
        mavenCentral()
        mavenLocal()
        maven{url 'https://m2.dv8tion.net/releases'}
        maven{url 'https://oss.sonatype.org/content/repositories/snapshots'}
        maven{url 'https://www.jitpack.io'}
    }

    dependencies{
        implementation "com.discord4j.discord4j:discord4j-core:master-SNAPSHOT"

        compileOnly "org.immutables:value:$immutables_version"
        annotationProcessor "org.immutables:value:$immutables_version"
        annotationProcessor "com.austinv11.servicer:Servicer:$servicer_version"

        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    }

    tasks.withType(JavaCompile){
        options.encoding = "UTF-8"
        options.incremental = true
        options.compilerArgs += ["-Xlint:deprecation"]
    }

    tasks.withType(Test){
        useJUnitPlatform()
    }
}

dependencies{
    compileOnly project(':encoding')
    if(findProject(':legacy') != null){
        // Module with outdated, test code
        api project(':legacy')
    }

    implementation "com.discord4j.discord4j:discord4j-voice:master-SNAPSHOT"

    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "com.udojava:EvalEx:$evalex_version"
    implementation "com.sedmelluq:lavaplayer:$lavaplayer_version"

    // data dependencies
    runtimeOnly "org.postgresql:postgresql"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version"

    // spring dependencies
    implementation "org.springframework.boot:spring-boot-starter-quartz"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.immutables:builder:$immutables_version"
}

plugins{
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'java-library'
}

ext{
    caffeine_version = '3.0.4'
    evalex_version = '2.6'

    immutables_version = '2.8.8'
    jackson_version = '2.12.5'
    d4j_json_version = '1.7.1-SNAPSHOT'

    junit_version = '5.8.1'
}

allprojects{
    apply plugin: 'java-library'
    apply plugin: 'com.github.ben-manes.versions'

    targetCompatibility = 16
    sourceCompatibility = 16

    repositories{
        mavenCentral()
        mavenLocal()
        maven{url 'https://oss.sonatype.org/content/repositories/snapshots'}
        maven{url 'https://www.jitpack.io'}
    }

    dependencies{
        implementation "com.discord4j:discord4j:master-SNAPSHOT"

        testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    }

    tasks.withType(JavaCompile){
        options.encoding = "UTF-8"
        options.incremental = true
        options.compilerArgs += ["-Xlint:deprecation"]
    }

    tasks.withType(Test){
        useJUnitPlatform()
    }

    configurations.all{
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies{
    if(findProject(':legacy') != null){
        // Module with outdated, test code
        api project(':legacy')
    }

    compileOnly "com.discord4j:discord-json-encoding:$d4j_json_version"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "com.udojava:EvalEx:$evalex_version"

    // data dependencies
    runtimeOnly "org.postgresql:postgresql"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jackson_version"

    // spring dependencies
    implementation "org.springframework.boot:spring-boot-starter-quartz"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    compileOnly "org.immutables:builder:$immutables_version"
    compileOnly "org.immutables:value:$immutables_version"
    annotationProcessor "org.immutables:value:$immutables_version"
}
